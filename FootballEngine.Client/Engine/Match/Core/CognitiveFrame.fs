namespace FootballEngine

open FootballEngine.Domain
open FootballEngine.PhysicsContract
open SimStateOps
open MatchSpatial

module CognitiveFrameModule =
    let build (ctx: MatchContext) (state: SimState) (clubSide: ClubSide) : CognitiveFrame =
        let ownFrame = getFrame state clubSide
        let oppFrame = getFrame state (ClubSide.flip clubSide)
        let ownRoster = getRoster ctx clubSide
        let n = ownFrame.SlotCount
        let m = oppFrame.SlotCount

        let buffers =
            let existing =
                if clubSide = HomeClub then state.HomeCFrameBuffers
                else state.AwayCFrameBuffers
            match existing with
            | Some buf when buf.NearestTeammateIdx.Length >= n ->
                Array.fill buf.NearestTeammateIdx 0 buf.NearestTeammateIdx.Length 0s
                Array.fill buf.NearestTeammateDistSq 0 buf.NearestTeammateDistSq.Length System.Single.MaxValue
                Array.fill buf.NearestOpponentIdx 0 buf.NearestOpponentIdx.Length 0s
                Array.fill buf.NearestOpponentDistSq 0 buf.NearestOpponentDistSq.Length System.Single.MaxValue
                Array.fill buf.BestPassTargetIdx 0 buf.BestPassTargetIdx.Length -1s
                Array.fill buf.BestPassTargetPos 0 buf.BestPassTargetPos.Length ValueNone
                Array.fill buf.PressureOnPlayer 0 buf.PressureOnPlayer.Length System.Single.MaxValue
                buf
            | _ ->
                let newBuf = CognitiveFrameBuffers.create n
                if clubSide = HomeClub then state.HomeCFrameBuffers <- Some newBuf
                else state.AwayCFrameBuffers <- Some newBuf
                newBuf

        let nearestTMIdx = buffers.NearestTeammateIdx
        let nearestTMDistSq = buffers.NearestTeammateDistSq
        let nearestOppIdx = buffers.NearestOpponentIdx
        let nearestOppDistSq = buffers.NearestOpponentDistSq
        let bestPassIdx = buffers.BestPassTargetIdx
        let bestPassPos = buffers.BestPassTargetPos
        let pressure = buffers.PressureOnPlayer

        let bx = float32 state.Ball.Position.X
        let by = float32 state.Ball.Position.Y
        let dir = attackDirFor clubSide state
        let ballZone = ofBallX state.Ball.Position.X dir
        let phase = phaseFromBallZone dir state.Ball.Position.X

        let ballCarrierOppIdx =
            match state.Ball.Possession with
            | Owned(side, pid) when side <> clubSide ->
                let mutable found = -1s
                let oppFrame = getFrame state (ClubSide.flip clubSide)
                let oppRoster = getRoster ctx (ClubSide.flip clubSide)
                for i = 0 to oppFrame.SlotCount - 1 do
                    match oppFrame.Occupancy[i] with
                    | OccupancyKind.Active rosterIdx when oppRoster.Players[rosterIdx].Id = pid -> found <- int16 i
                    | _ -> ()
                found
            | _ -> -1s

        for i = 0 to n - 1 do
            match ownFrame.Occupancy[i] with
            | OccupancyKind.Active _ ->
                let ox = ownFrame.PosX[i]
                let oy = ownFrame.PosY[i]

                let mutable minTMDistSq = System.Single.MaxValue
                let mutable minTMIdx = int16 -1s
                for j = 0 to n - 1 do
                    if i <> j then
                        match ownFrame.Occupancy[j] with
                        | OccupancyKind.Active _ ->
                            let dx = ownFrame.PosX[j] - ox
                            let dy = ownFrame.PosY[j] - oy
                            let d = dx * dx + dy * dy
                            if d < minTMDistSq then
                                minTMDistSq <- d
                                minTMIdx <- int16 j
                        | _ -> ()
                nearestTMIdx[i] <- minTMIdx
                nearestTMDistSq[i] <- minTMDistSq

                let mutable minOppDistSq = System.Single.MaxValue
                let mutable minOppIdx = int16 -1s
                for j = 0 to m - 1 do
                    match oppFrame.Occupancy[j] with
                    | OccupancyKind.Active _ ->
                        let dx = oppFrame.PosX[j] - ox
                        let dy = oppFrame.PosY[j] - oy
                        let d = dx * dx + dy * dy
                        if d < minOppDistSq then
                            minOppDistSq <- d
                            minOppIdx <- int16 j
                    | _ -> ()
                nearestOppIdx[i] <- minOppIdx
                nearestOppDistSq[i] <- minOppDistSq
                pressure[i] <- minOppDistSq

                let bestPass =
                    findBestPassTargetFrame i ownFrame ownRoster oppFrame dir
                match bestPass with
                | ValueSome (idx, sp) ->
                    bestPassIdx[i] <- int16 idx
                    bestPassPos[i] <- ValueSome sp
                | ValueNone -> ()

            | _ -> ()

        { NearestTeammateIdx = nearestTMIdx
          NearestTeammateDistSq = nearestTMDistSq
          NearestOpponentIdx = nearestOppIdx
          NearestOpponentDistSq = nearestOppDistSq
          BestPassTargetIdx = bestPassIdx
          BestPassTargetPos = bestPassPos
          BallX = bx
          BallY = by
          BallZone = ballZone
          Phase = phase
          PressureOnPlayer = pressure
          SlotCount = n
          BallCarrierOppIdx = ballCarrierOppIdx }
