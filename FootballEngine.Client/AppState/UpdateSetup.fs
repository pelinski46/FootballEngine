namespace FootballEngine

open Elmish
open FootballEngine.Domain
open AppTypes
open AppMsgs
open FootballEngine.Generation.WorldGen

module UpdateSetup =

    let private saveCmd = SimHelpers.saveCmd

    let handle (msg: SetupMsg) (state: State) : State * Cmd<Msg> =
        match msg with
        | GoToStep step -> { state with State.Setup.Step = step }, Cmd.none

        | SelectPrimaryCountry code ->
            { state with
                Setup =
                    { state.Setup with
                        SelectedCountry = Some code
                        SecondaryCountries = state.Setup.SecondaryCountries |> List.filter ((<>) code) } },
            Cmd.none

        | ToggleSecondaryCountry code ->
            if state.Setup.SelectedCountry = Some code then
                state, Cmd.none
            else
                let updated =
                    if List.contains code state.Setup.SecondaryCountries then
                        state.Setup.SecondaryCountries |> List.filter ((<>) code)
                    else
                        code :: state.Setup.SecondaryCountries

                { state with
                    State.Setup.SecondaryCountries = updated },
                Cmd.none

        | UpdateManagerName name ->
            { state with
                State.Setup.ManagerName = name },
            Cmd.none

        | StartNewGame ->
            match state.Setup.SelectedCountry with
            | None -> state, Cmd.none
            | Some primaryCountry ->
                let newGameState =
                    generateNewGame primaryCountry state.Setup.ManagerName state.Setup.SecondaryCountries

                { state with
                    GameState = newGameState
                    State.Setup.Step = ClubSelection },
                saveCmd newGameState

        | ConfirmClub clubId ->
            let updatedStaff =
                state.GameState.Staff
                |> Map.tryFind state.GameState.UserStaffId
                |> Option.map (fun manager ->
                    { manager with
                        Contract =
                            Some
                                { ClubId = clubId
                                  Salary = 50_000m
                                  ExpiryYear = state.GameState.Season + 3 } })
                |> Option.map (fun manager -> state.GameState.Staff |> Map.add manager.Id manager)
                |> Option.defaultValue state.GameState.Staff

            let newGameState =
                { state.GameState with
                    UserClubId = clubId
                    Staff = updatedStaff }

            let managerName = GameState.userManagerName newGameState

            { state with
                GameState = newGameState
                CurrentPage = HomePage
                LogMessages = [ $"Career started by {managerName}" ] },
            saveCmd newGameState
